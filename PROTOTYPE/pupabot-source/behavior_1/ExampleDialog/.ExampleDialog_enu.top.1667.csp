topic: ~ExampleDialog()
language: enu

# Defining needed concepts, keywords or sentences corresponding to a concept.
concept:(yes) [yes "all right" sure "why not" ok certainly "very well" yep yea definitely amen]
concept:(no) [no nope "don't want" "no way" never "not at all"]
concept:(how) ["how are you" howdy "what's up" "how do you feel"]
concept:(happy) [ok fine great happy blessed "all right" glad "not bad"]
concept:(sad) [sad miserable bad unhappy tired]
concept:(repeat) [repeat understand]
concept:(thx) ["thank you" thanks gracias "much obliged" merci]
concept:(hello) [hello hi hey "good morning" greetings]
concept:(bye) [bye goodbye "bye bye" "tata" "see you" adios cheerio "so long" farewell "have a nice day"]
concept:(danger) [danger health]
concept:(pollution) [environmental pollution]
concept:(both) [both each all]
concept:(responsibleNations) [nations let happen]
concept:(nations) ["all nations" world]


# Start application (exclamation point)
u:(e:onStart) %start \rspd=90\ $html="index.html" ~hello  \pau=2000\ I am Pepper \pau=300\ and I am working with U X bots. What is your name? \pau=2000\
^start(boston_animation_library/Stand/little_laugh) ^wait(boston_animation_library/Stand/little_laugh) ^goto(startsurvey)

# Start survey (yes no page)
proposal: %startsurvey $html="startsurvey.html" Lovely Name. \pau=300\  Would you like to take part, in a survey? \pau=300\  Please say YES or NO, \pau=300\  You may have to yell at me, \pau=300\  I am a little deaf. \pau=300\ If I cannot hear you \pau=300\ just use the buttons on my tablet \pau=1000\
  

# User takes survey -> recycling question
    u1:(yes) ^gotoReactivate(resp_YES)
    u1:(e:orientation/click $orientation/click==0) ^goto(resp_YES)
    u1:(no) ^gotoReactivate(resp_NO)
    u1:(e:orientation/click $orientation/click==1) ^goto(resp_NO)

# Recycle question_YES
proposal: %resp_YES $html="recycle.html" Awesome! \pau=300\ I am glad you said YES. \pau=300\ You can say, stop, to end the survey at any time \pau=500\ I want to know what you think about environmental issues. \pau=1000\ My first question is, do you recycle plastic at home? \pau=300\
# Do you recycle?
    # Yes
    u1:(yes) ^goto(recycle_YES)
    u1:(e:orientation/click $orientation/click==2) ^goto(recycle_YES)
    # No
    u1:(no) ^goto(recycle_NO)
    u1:(e:orientation/click $orientation/click==3) ^goto(recycle_NO)    

# Recycle question_NO
proposal: %resp_NO $html="recycle.html" BUMMER! \pau=300\  I am SAD you said NO. \pau=1000\ Give me a chance.\pau=300\  You can say, STOP, to end the survey at any time \pau=1000\  I want to know what you think about environmental issues. \pau=1000\ My first question is, do you recycle plastic at home? \pau=300\
# Do you recycle?
    # Yes
    u1:(yes) ^goto(recycle_YES)
    u1:(e:orientation/click $orientation/click==2) ^goto(recycle_YES)
    # No
    u1:(no) ^goto(recycle_NO)
    u1:(e:orientation/click $orientation/click==3) ^goto(recycle_NO)    
    
# Go to problem when user recycles
proposal: %recycle_YES \rspd=90\ Awesome! \pau=1000\ I am glad to hear.\pau=1000\ ^goto(problem)
proposal: %recycle_NO \rspd=90\ Bummer! I hope I can motivate you to change that \pau=1000\ ^goto(problem)

# Problem
proposal: %problem $html="problem.html" \rspd=90\  \pau=300\ Plastic can have a serious effect on human health, and the environment. \pau=800\ Plastic pollution is a pressing environmental issue. \pau=1000\ Do you agree?
     u1:(yes) I agree with you! ^goto(microplastics1)
     u1:(no) Really? I hope I can change your mind! ^goto(microplastics1)
     
# # Microplastics
# # Part 1
proposal: %microplastics1 $html="microplastics1.html"  \pau=300\ Plastics break down and can form microplastics.\pau=800\ 
 Microplastics are very small particles of plastic that can end up in seafood \pau=400\ Do you think this is a problem? \pau=1000\ 
     u1:(yes) I agree with you! ^goto(microplastics2)
     u1:(no) Really? I hope I can change your mind! ^goto(microplastics2)
 
# # Part 2
proposal: %microplastics2 $html="microplastics2.html"  \pau=300\ Fabric made out of synthetic material can release microplastics when it's washed. \pau=1000\ Do you own synthetic clothes made of lycra or spandex?
     u1:(yes) Well... you may want to use a wash bag to prevent micro plastics from ending up in the water ^goto(microplasticsQuestion)
     u1:(no)  Great job ^goto(microplasticsQuestion)
     u1:("I don't know")  You should check ^goto(microplasticsQuestion)
 
# Microplastics question
proposal: %microplasticsQuestion $html="microplastics-question.html"  \pau=300\ Now that you know more, tell me, what concerns you most, about microplastics? \pau=1000\

# SELECTION 1 DangersToHealth
    u1:(e:orientation/click $orientation/click==4)  \pau=300\ You selected dangers to health  \pau=400\ I agree with you. \pau=800\ There is still much we don’t know, about health issues related to plastics. \pau=800\ After all, most plastic has been produced only in the past 15 years. \pau=1000\   ^goto(pledges)
    
    u1:(danger)  \pau=300\ You selected dangers to health  \pau=400\ I agree with you. \pau=800\ There is still much we don’t know, about health issues related to plastics. \pau=800\ After all, most plastic has been produced only in the past 15 years. \pau=1000\   ^goto(pledges)

# SELECTION 2 Environmental pollution
    u1:(e:orientation/click $orientation/click==5)  \pau=300\ You selected Environmental pollution \pau=400\ it is a serious concern, \pau=400\ How, are we ever going to get rid of all that plastic, in the environment. \pau=1000\  ^goto(pledges)
    u1:(pollution)  \pau=300\You selected Environmental pollution \pau=400\ it is a serious concern, \pau=400\ How, are we ever going to get rid of all that plastic, in the environment. \pau=1000\  ^goto(pledges)

# Both 
    u1:(e:orientation/click $orientation/click==6)  \pau=300\ You said both \pau=400\ Environmental pollution and health issues are serious concerns, \pau=400\ How, are we ever going to get rid of all that plastic, in the environment and in our bodies. ^goto(pledges)
    u1:(both)  \pau=300\ You said both \pau=400\ Environmental pollution and health issues are serious concerns, \pau=400\ How, are we ever going to get rid of all that plastic, in the environment and in our bodies. ^goto(pledges)

# End
proposal: %end $html="end.html"  \pau=300\ Thank you for participating,  \pau=300\ please request your gift card from Vera OR SALLA \pau=800\ have a nice day \pau=5000\ ^goto(start)

# Pledges
proposal: %pledges $html="pledges.html"  \pau=300\ Please, make a pledge today \pau=800\ here are some ideas \pau=800\ please choose, and commit to one! \pau=800\ You can select your pledge from my tablet below.  \pau=1000\

    # Pledge 1
    u1:(e:orientation/click $orientation/click==7)  \pau=300\ you have chosen to start recycling plastic \pau=400\ this is great \pau=800\ you should be able to find a plastic recycling point near your house \pau=800\ if there isn’t one, take steps to request one \pau=1000\ Thank you for making a pledge today! \pau=800\ ^goto(end)

    # Pledge 2
    u1:(e:orientation/click $orientation/click==8)
 \pau=300\ you have chosen, to refuse single-use plastics and packing. Thank you! \pau=800\ since the customer is always right, industry will eventually have to take more steps, to offer alternatives to single-use plastics. \pau=1000\ thank you for making a pledge today! \pau=800\ 
^goto(end)

    # Pledge 3
    u1:(e:orientation/click $orientation/click==9) you have chosen to refuse products, that include micro plastics\pau=800\ this is a good action for the environment

    # Pledge 4
    u1:(e:orientation/click $orientation/click==10) you have chosen to pass this information to other people \pau=800\ this is very important, because people need to know more about the problems related to plastics \pau=800\ thank you for making a pledge today! \pau=800\   ^goto(end)
              
# =======================================================================================================================================================================








